plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.google.protobuf' version '0.9.4'
}

group = 'ru.shilaev'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Для облегчения разработки
    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'

    // Для работы с базой данных
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    runtimeOnly 'org.postgresql:r2dbc-postgresql'
    // implementation 'org.springframework.boot:spring-boot-starter-data-jpa:3.3.2'
    // runtimeOnly 'org.postgresql:postgresql:42.7.3'

    // Для веб-разработки
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.grpc:spring-grpc-spring-boot-starter'

    // Для работы с протоколами и аннотациями
    implementation 'com.google.protobuf:protobuf-java:4.29.3'
    implementation 'io.grpc:protoc-gen-grpc-java:1.71.0'
    implementation 'jakarta.annotation:jakarta.annotation-api:3.0.0'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    // Для работы с документами
    implementation 'org.apache.poi:poi-ooxml:5.4.0'
    implementation 'org.apache.poi:poi:5.4.0'



    // Для валидации
    implementation 'org.springframework.boot:spring-boot-starter-validation:3.3.2'

    // Для тестирования
    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.3.2'
    testImplementation 'io.projectreactor:reactor-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Т-Инвест API
    implementation 'ru.tinkoff.piapi:java-sdk-core:1.27'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.grpc:spring-grpc-dependencies:0.3.0"
    }
}

sourceSets {
    main {
        proto {
            srcDirs 'src/main/resources/proto'
        }
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}


protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:4.29.3'
    }

    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.70.0'
        }
    }

    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
